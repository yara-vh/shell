pragma ComponentBehavior: Bound

import QtQuick
import Quickshell
import Caelestia
import Caelestia.Config
import Caelestia.Services
import qs.components
import qs.components.controls
import qs.services
import qs.utils

Column {
    id: root

    required property ScreenState screenState

    padding: Tokens.padding.large
    rightPadding: CUtils.clamp(padding - Config.border.thickness, 0, padding)
    spacing: Tokens.spacing.large

    SessionButton {
        id: shutdown

        icon: "power_settings_new"
        command: Config.session.commands.shutdown

        KeyNavigation.up: logout
        KeyNavigation.down: reboot

        Component.onCompleted: forceActiveFocus()
    }

    SessionButton {
        id: reboot

        icon: "restart_alt"
        command: Config.session.commands.reboot

        KeyNavigation.up: shutdown
        KeyNavigation.down: hibernate
    }

    SessionButton {
        id: hibernate

        isToggle: true // get rid of ugly outline

        icon: "bedtime"
        command: Config.session.commands.hibernate

        KeyNavigation.up: reboot
        KeyNavigation.down: logout
    }

    // SessionButton {
    //     id: logout
    //
    //     icon: Config.session.icons.logout
    //     command: Config.session.commands.logout
    //
    //     KeyNavigation.up: hibernate
    //     KeyNavigation.down: shutdown
    //
    //     Connections {
    //         function onLauncherChanged(): void {
    //             if (!root.screenState.launcher)
    //             logout.forceActiveFocus();
    //         }
    //
    //         target: root.screenState
    //     }
    // }

    // AnimatedImage {
    //     width: Tokens.sizes.session.button
    //     height: Tokens.sizes.session.button
    //     sourceSize.width: width * ((QsWindow.window as QsWindow)?.devicePixelRatio ?? 1)
    //
    //     playing: visible
    //     asynchronous: true
    //     speed: Config.general.sessionGifSpeed
    //     source: Paths.absolutePath(Config.paths.sessionGif)
    //     fillMode: AnimatedImage.PreserveAspectFit
    // }

    component SessionButton: IconButton {
        id: button

        required property list<string> command

        function exec(): void {
            if (!SessionManager.exec(command))
            Quickshell.execDetached(command);
        }

        implicitWidth: Tokens.sizes.session.button
        implicitHeight: Tokens.sizes.session.button

        inactiveColour: activeFocus ? Colours.palette.m3secondaryContainer : Colours.tPalette.m3surfaceContainer
        inactiveOnColour: activeFocus ? Colours.palette.m3onSecondaryContainer : Colours.palette.m3onSurface
        radius: pressed ? Tokens.rounding.medium : activeFocus ? Tokens.rounding.extraLarge : Tokens.rounding.largeIncreased
        font: Tokens.font.icon.builders.large.scale(1.3).build()
        onClicked: exec()

        Keys.onEnterPressed: exec()
        Keys.onReturnPressed: exec()
        Keys.onEscapePressed: root.screenState.session = false
        Keys.onPressed: event => {
            if (!Config.session.vimKeybinds)
            return;

            if (event.modifiers & Qt.ControlModifier) {
                if ((event.key === Qt.Key_J || event.key === Qt.Key_N) && KeyNavigation.down) {
                    KeyNavigation.down.focus = true;
                    event.accepted = true;
                } else if ((event.key === Qt.Key_K || event.key === Qt.Key_P) && KeyNavigation.up) {
                    KeyNavigation.up.focus = true;
                    event.accepted = true;
                }
            } else if (event.key === Qt.Key_Tab && KeyNavigation.down) {
                KeyNavigation.down.focus = true;
                event.accepted = true;
            } else if (event.key === Qt.Key_Backtab || (event.key === Qt.Key_Tab && (event.modifiers & Qt.ShiftModifier))) {
                if (KeyNavigation.up) {
                    KeyNavigation.up.focus = true;
                    event.accepted = true;
                }
            }
        }
    }
}
